plugins {
    id 'com.android.application'
//    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {
    namespace VersionConfig.packageName
    compileSdk VersionConfig.compileSdkVersion

    defaultConfig {
        applicationId VersionConfig.packageName
        minSdk VersionConfig.minSdkVersion
        targetSdk VersionConfig.targetSdkVersion
        versionCode VersionConfig.versionCode
        versionName VersionConfig.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        releaseConfig {
            storeFile file('../config/pgphone.jks')
            storePassword 'example_pgphone'
            keyAlias 'examplePgPhone'
            keyPassword 'example_pgphone'
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.releaseConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.releaseConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        forTest {
            initWith release
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    sourceSets {
        main {
            assets {
                srcDirs 'src\\main\\assets'
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def sourceFile = "pgp_v${defaultConfig.versionName}_${variant.buildType.name}_${releaseTime()}.apk"
            outputFileName = sourceFile
        }
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    api fileTree(include: ['*.jar'], dir: 'libs')
    testImpl.each {
        testImplementation it.value
    }
    androidTestImplBase.each {
        androidTestImplementation it.value
    }
    implementationBase.each {
        implementation it.value
    }

    // --- 三方sdk --- //
    // oaid
    implementation files('libs/oaid_sdk_1.0.25.aar')
    implementation project(":library_network")
    implementation project(":library_qrcode")
}